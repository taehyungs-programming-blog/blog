---
layout: default
title: "2. 어셈블리 궁금했던 점 정리"
parent: (Windows API)
grand_parent: C++
nav_order: 1
---

## Table of contents
{: .no_toc .text-delta }

1. TOC
{:toc}

---

## 함수이름은 어셈블리 내에서 어떻게 정의될까?

```cpp
#include <stdio.h>
// 예를 들어 어셈블리로 asm_main를 만드려고한다
int asm_main();     // 어셈블리 파일로 만들예정
int main()
{
    int n = asm_main();
    printf("result : %d\n", n);
}
```

```cpp
; asm1.asm
.model flat
public _asm_main
.code
_asm_main:
    mov     eax, 100        ; return 100과 동일
    ret
end     ; 파일의 끝에는 항상 end가 있어야 한다.
```

<Br>

😺 cpp에서는 함수의 이름을 `asm_main()`로 선언했는데 어셈블리에선 `_asm_main`라 정의한 이유??
    * cl컴파일러는 함수의 이름 앞에 `_`를 붙여서 쓴다. cl컴파일러의 규칙을 따르기 위해서 추가함.

---

## 어셈블리 파일에서 `.model {option}`는 뭘 의미하나?

* .model은 해당 어셈블리 파일의 정보를 알려준다
    * memory model : 32bit 응용프로그램의 경우 flat으로 정의 하고 16bit는 여러가지가 있는데 16는 쓰일일이 없으니 설명은 생략한다.
    * language-type : c, stdcall로 정의가능한데 이름으로 봐서 알겠지만 calling-convention을 지정하는 것. (콜링 컨벤션에 대한 자세한 내용은 다음강 참조! 여기서 설명하긴 길다)
    * stack-option : 32bit에서는 사용이 안되고 16bit에서는 여러 설정이 있지만 역시 설명은 생략

```
; Example
.model flat             ;
.model flat, c          ; language-type을 c로
.model float, stdcall   ; language-type을 stdcall로
```

* [langtype(language-type)에 관하여](https://stackoverflow.com/questions/38284619/what-is-the-semantics-of-langtypes-c-and-stdcall-for-model-in-masm-assembly)
* [langtype(language-type)에 관하여2](https://slideplayer.com/slide/12468278/)
* [langtype(language-type)에 관하여3](https://social.msdn.microsoft.com/forums/vstudio/en-US/552bbb2c-2d27-41d8-9cf8-09d2ef552a0b/masm-help-1)

---

## 빌드가 완료되고 생성된 실행파일의 구조는 어떨까?

😺 실행파일의 구조를 PE Format이라 하며 대략적으로 아래와 같이 생성된다.

```
Header
-------
Section .text (함수가 들어있다)
Section .data (전역 변수가 들어있다)
...
```

<br>

😺 물론 .exe를 열어보면 `Section .text`, `Section .data`말고 훨씬 많지만 한 번에 모든걸 설명할 수 없으니 하나하나 설명한다.

😺 대략적으로 `Section .text`, `Section .data`는 어셈블리 파일에서 아래와 같다

```
.model flat
public _asm_main ; 다른 함수에서도 부르게 해달라는 명령(c에서 부를수 있게 된다.)
.data
; Section .data(변수) 를 의미한다
; 전역 변수이다
.code   ; .text(함수) 를 의미한다.
_asm_main:
    mov     eax, 100    ; (참고) eax레지스터는 보통 반환값을 넣는 용도로 사용됨
    ret ; 함수 종료(return)를 의미
end
```

* (참고) PE에 관해서는 이후 [DLL설명]()에서 추가설명을 넣음.
    * 일단 PE Format이라는게 바이너리가 `Header`, `Section .text(함수)`, `Section .data(변수)`로 구분된다고 기억하자. --> 요것만 알아도 100점!

---

## 어셈블리의 또 다른 표기법

😺 개발자 혹은 IDE(컴파일러)에 따라 표기법이 달라 질 수 있으니 참고하자

```
.model flat
public _asm_main
.code
_asm_main proc  ; 콜론(:)대신 proc표기가능 
    mov     eax, 100
    ret
_asm_main endp  ; proc가 끝났다고 알린다
end
```

```
.model flat
public _asm_main
_DATA SEGMENT   ; .data와 동일
_DATA ends
_TEXT SEGMENT    ; .code 대신에 _TEXT SEGMENT 표기가능
_asm_main proc
    mov     eax, 100
    ret
_asm_main endp
_TEXT ends
end
```

---

## `.c` 파일을 어셈블리 파일로 뽑고 싶다

```bash
# asm1.c를 어셈블리 언어로 표현해주세요
$ cl asm1.c /FAs /c
# /FAs : 어셈블리를 만들어 달라
# /c : 컴파일만 해달라
```

---

## Data section 정리

```
.model flat
public _asm_main
.data
L1  DWORD   100     ; 4바이트 할당 100을 넣어라
L2  DD      200     ; DD = DWORD와 동일어
L3  DD      ?       ; 초기값을 정의하고 싶지 않다면 ?를 넣자
.code
_asm_main:
    mov     eax, L1 ; 100이 리턴
    ; 메모리 복사
    ; mov     L1, L2  ; 컴파일 에러 : 2개의 오퍼랜드가 모두 메모리일 수 없다.
    mov     ebx, L2
    mov     L1, ebx
    ; 이런식으로 해야함
    ; 메모리 주소값 구하기
    ; ebx = &L1
    mov     ebx, offset L1  ; L1의 메모리주소를 넣어라
                            ; ebx 에는 L1의 주소가 들어가게 된다
    mov     eax, [ebx]      ; []는 C의 *와 동일 *ebx(ebx가 가리키는 값 = L1값)
    ; ebx = 300
    ; mov     [ebx], 300            ; 에러 : 300을 몇 바이트(int, short)로 넣을지 미정
    mov     dword ptr[ebx], 300     ; dword(4바이트)로 넣어주세요
    ret
end
```

<br>

* [dword ptr에 관하여](https://stackoverflow.com/questions/2987876/what-does-dword-ptr-mean)

<br>

```
; 배열 만들어 보기
.model flat
public _asm_main
.data
; L1   DD   100
L1   DD   100, 200, 300, 400
L2   DD   4 dup (100) ; 100, 100, 100, 100 과 동일
; 4개의 100을 넣어달라
.code
_asm_main:
    ; mov eax, L1
    mov esi, offset L1          ; L1의 주소를 esi에 넣어달라
    mov eax, dword ptr[esi+4]   ; esi(L1)의 주소에서 +4 바이트한 데이터값을 eax로 넣어라
    ret
end
```

<br>

* [eax, ebx ?? 레지스터 종류에 관하여](https://m.blog.naver.com/PostView.naver?isHttpsRedirect=true&blogId=cung91&logNo=220196144355)

<br>

---

## 함수내에서 다른 함수를 호출하려면?

* 함수를 호출하는 방법
    * jmp
    * call

```
; Example
.model flat
public _asm_main
public _foo
.code
_asm_main:
    mov eax, 100
    ; 여기서 _foo를 호출하고 싶다면?
    ret
_foo:   ; 참고로 c에서도 호출하게 하고 싶어서 _를 앞에 붙임
end
```

```
_asm_main:
    mov eax, 100
    mov ebx, POS_A      ; 함수를 호출 후 POS_A로 돌아오게 해주세요
    ; mov ebx, 돌아올 주소
    jmp _add ; 함수 호출
POS_A:
    ret
_add:
    mov eax, 300
    jmp ebx             ; ebx에 리턴 후 돌아갈 주소가 남겨져 있기에 jmp하면 돌아감
```

<br>

😺 이렇게 구현시 발생하는 문제점 👉 ebx에 돌아갈 주소를 넣어둘 경우 ebx는 함수가 종료될때까지 사용하지 못하게된다.(메모리의 낭비라 할 수 있다)

```
_asm_main:
    mov eax, 100
    ; 스택에 쌓는다
    push POS_A
    jmp _add ; 함수 호출
POS_A:
    ret
_add:
    mov eax, 300
    pop ebx ; ebx에 pop해달라
    jmp ebx
```

<br>

😺 뭘 스택까지 써야하나?? 너무 복잡한데? 👉 call을 쓰자

```
_asm_main:
    mov eax, 100
    call _add   ; 스택에 넣고 돌아올 주소까지 할당
    ret
_add:
    mov eax, 300
    ret         ; 스택에 꼭대기에 돌아갈 주소가 있다고 가정
```

* 결론 : `call`, `ret`을 쓰자